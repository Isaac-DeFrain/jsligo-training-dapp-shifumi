#import "assert.mligo" "Assert"
#import "../contracts/errors.jsligo" "Errors"
#import "../contracts/main.jsligo" "Contract"

type player = {
    address: address,
    secret: nat
};

// prepare players
let _:unit = Test.reset_state((3 as nat),(list([]) as list<tez>));
let alice: player = { address : Test.nth_bootstrap_account(0), secret: 42 as nat };
let bob: player = { address : Test.nth_bootstrap_account(1), secret: 24 as nat };
let james: address = Test.nth_bootstrap_account(2);

// Contract origination
let get_contract = (storage:Contract.Storage.t) : contract<Contract.parameter> => {
    const originated = Test.originate(Contract.main, storage, (0 as tez));
    return (Test.to_contract(originated[0]) as contract<Contract.parameter>); 
};

// Shifumi turn
let get_play = ([play, secret]: [Contract.Storage.action, nat]) : [chest, chest_key] => {
    const payload = Bytes.pack(play);
    return Test.create_chest(payload, secret);
};

// ----------------------------------
// Test cases
// ----------------------------------

let case_alice_play = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));

    // When
    Test.set_source(alice.address);
    const material = get_play(Paper(), alice.secret);
    const result = Test.transfer_to_contract(contract, Play(material[0]), (0 as tez));
    
    // Then
    Assert.assert_success(result);

    return "✅";
};

let case_alice_play_twice = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));
    Test.set_source(alice.address);
    const material = get_play(Paper(), alice.secret);
    const _ = Test.transfer_to_contract(contract, Play(material[0]), (0 as tez));

    // When
    const result = Test.transfer_to_contract(contract, Play(material[0]), (0 as tez));
    
    // Then
    Assert.assert_string_failure(result, Errors.user_already_played);

    return "✅";
};


let case_alice_play_and_reveal = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));
    Test.set_source(alice.address);
    const material = get_play(Paper(), alice.secret);
    const _ = Test.transfer_to_contract(contract, Play(material[0]), (0 as tez));

    // When
    const result = Test.transfer_to_contract(contract, Reveal([material[1], alice.secret]), (0 as tez));
    
    // Then
    Assert.assert_string_failure(result, Errors.user_not_allowed_to_reveal);
    
    return "✅";
};

let case_alice_reveal = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));
    Test.set_source(alice.address);
    const material = get_play(Paper(), alice.secret);

    // When
    const result = Test.transfer_to_contract(contract, Reveal([material[1], alice.secret]), (0 as tez));
    
    // Then
    Assert.assert_string_failure(result, Errors.user_not_allowed_to_reveal);
    
    return "✅";
};

let case_alice_play_bob_play = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));
    Test.set_source(alice.address);
    const material_alice = get_play(Paper(), alice.secret);
    const _ = Test.transfer_to_contract(contract, Play(material_alice[0]), (0 as tez));

    // When
    Test.set_source(bob.address);
    const material_bob = get_play(Cisor(), bob.secret);
    const result = Test.transfer_to_contract(contract, Play(material_bob[0]), (0 as tez));
  
    // Then
    Assert.assert_success(result);
    
    return "✅";
};

let case_alice_play_bob_play_alice_reveal = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));
    Test.set_source(alice.address);
    const material_alice = get_play(Paper(), alice.secret);
    const _ = Test.transfer_to_contract(contract, Play(material_alice[0]), (0 as tez));

    Test.set_source(bob.address);
    const material_bob = get_play(Cisor(), bob.secret);
    const _ = Test.transfer_to_contract(contract, Play(material_bob[0]), (0 as tez));

    // When
    Test.set_source(alice.address);
    const result = Test.transfer_to_contract(contract, Reveal([material_alice[1], alice.secret]), (0 as tez));

    // Then
    Assert.assert_success(result);
    
    return "✅";
};

let case_alice_play_bob_play_alice_reveal_bob_reveal = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));
    Test.set_source(alice.address);
    const material_alice = get_play(Paper(), alice.secret);
    const _ = Test.transfer_to_contract(contract, Play(material_alice[0]), (0 as tez));

    Test.set_source(bob.address);
    const material_bob = get_play(Cisor(), bob.secret);
    const _ = Test.transfer_to_contract(contract, Play(material_bob[0]), (0 as tez));

    Test.set_source(alice.address);
    const result = Test.transfer_to_contract(contract, Reveal([material_alice[1], alice.secret]), (0 as tez));

    // When
    Test.set_source(bob.address);
    const result = Test.transfer_to_contract(contract, Reveal([material_bob[1], bob.secret]), (0 as tez));

    // Then
    Assert.assert_success(result);
    
    return "✅";
};


let case_james_cannot_play = () : string => {
    // Given
    const contract = get_contract(Contract.Storage.create(alice.address, bob.address));

    // When
    Test.set_source(james);
    const material = get_play(Paper(), 0 as nat);
    const result = Test.transfer_to_contract(contract, Play(material[0]), (0 as tez));

    // Then
    Assert.assert_string_failure(result, Errors.user_not_allowed_to_play_or_reveal)
    
    return "✅";
};

let test_case_alice_play = case_alice_play ();
let test_case_alice_play_twice = case_alice_play_twice ();
let test_case_alice_play_and_try_reveal = case_alice_play_and_reveal ();
let test_case_alice_reveal = case_alice_reveal ();
let test_case_alice_play_bob_play = case_alice_play_bob_play ();
let test_case_alice_play_bob_play_alice_reveal = case_alice_play_bob_play_alice_reveal ();
let test_case_alice_play_bob_play_alice_reveal_bob_reveal = case_alice_play_bob_play_alice_reveal_bob_reveal ();
let test_case_james_cannot_play = case_james_cannot_play ()